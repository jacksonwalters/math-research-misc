{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "eb354aab",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "F = GF(2)\n",
    "V = VectorSpace(F, 3)\n",
    "G = GL(3, GF(2))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "e6663c29",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "7"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "points = [U for U in V.subspaces(1)]\n",
    "len(points)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "7defe5c4",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "7"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "planes = [U for U in V.subspaces(2)]\n",
    "len(planes)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "feccb248",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "point_index = {points[i]: i for i in range(len(points))}\n",
    "plane_index = {planes[i]: i for i in range(len(planes))}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "af936547",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "21"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "edges = []\n",
    "for p in points:\n",
    "    for H in planes:\n",
    "        if p.is_subspace(H):\n",
    "            edges.append((p, H))\n",
    "len(edges)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "0c1f7fc0",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "edge_index = {edges[i]: i for i in range(len(edges))}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "46d94fc3",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "num_vertices = len(points) + len(planes)\n",
    "num_edges = len(edges)\n",
    "\n",
    "boundary = Matrix(CDF, num_vertices, num_edges)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "cb6786d2",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "for (p, H), j in edge_index.items():\n",
    "    i_p = point_index[p]\n",
    "    i_H = plane_index[H] + len(points)\n",
    "\n",
    "    boundary[i_H, j] = 1     # +H\n",
    "    boundary[i_p, j] = -1    # -p"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "ed5ae17d",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "8"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "ker = boundary.right_kernel()\n",
    "ker.dimension()\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "bf7e5860",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Vector space of degree 21 and dimension 8 over Complex Double Field\n",
       "Basis matrix:\n",
       "[ 1.0  0.0 -1.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0 -1.0  0.0  1.0  0.0  0.0  0.0  1.0  0.0 -1.0]\n",
       "[ 0.0  1.0 -1.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0 -1.0  0.0  1.0  1.0  0.0 -1.0]\n",
       "[ 0.0  0.0  0.0  1.0  0.0 -1.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0 -1.0  1.0  0.0  0.0  0.0  1.0  0.0 -1.0]\n",
       "[ 0.0  0.0  0.0  0.0  1.0 -1.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0 -1.0  1.0  1.0  0.0 -1.0]\n",
       "[ 0.0  0.0  0.0  0.0  0.0  0.0  1.0  0.0 -1.0  0.0  0.0  0.0 -1.0  0.0  1.0  0.0  0.0  0.0  0.0  1.0 -1.0]\n",
       "[ 0.0  0.0  0.0  0.0  0.0  0.0  0.0  1.0 -1.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0 -1.0  1.0  0.0  1.0 -1.0]\n",
       "[ 0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  1.0  0.0 -1.0  0.0  0.0  0.0 -1.0  0.0  1.0  0.0  1.0 -1.0]\n",
       "[ 0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  1.0 -1.0  0.0 -1.0  1.0  0.0  0.0  0.0  0.0  1.0 -1.0]"
      ]
     },
     "execution_count": 10,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "ker"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "a37031e1",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(1.0, 0.0, -1.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, -1.0, 0.0, 1.0, 0.0, 0.0, 0.0, 1.0, 0.0, -1.0)"
      ]
     },
     "execution_count": 11,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "steinberg_basis = ker.basis()\n",
    "steinberg_basis[0]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "a45b2658",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "def line_repr(L):\n",
    "    \"\"\"\n",
    "    Canonical representative for a 1-dim subspace over F_2:\n",
    "    return its unique nonzero vector.\n",
    "    \"\"\"\n",
    "    for v in L.basis():\n",
    "        if v != 0:\n",
    "            return tuple(v)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "968fe1fa",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "def linear_form_str(a):\n",
    "    \"\"\"\n",
    "    a is a vector in F_2^3, printed as a linear equation a·x = 0\n",
    "    \"\"\"\n",
    "    terms = []\n",
    "    for i, ai in enumerate(a):\n",
    "        if ai != 0:\n",
    "            terms.append(f\"x{i+1}\")\n",
    "    if not terms:\n",
    "        return \"0\"\n",
    "    return \" + \".join(terms)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "64e7278e",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "def plane_repr(H):\n",
    "    \"\"\"\n",
    "    Represent a plane as a readable equation like x1 + x2 = 0\n",
    "    \"\"\"\n",
    "    V = H.ambient_vector_space()\n",
    "    for a in V:\n",
    "        if a != 0:\n",
    "            if all(a * v == 0 for v in H.basis()):\n",
    "                return \"{\" + linear_form_str(a) + \" = 0}\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "94c28178",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "def edge_repr(edge):\n",
    "    p, H = edge\n",
    "    return f\"{line_repr(p)} ⊂ {plane_repr(H)}\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "13787d79",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "def pretty_print_cycle(v, edges):\n",
    "    for j, coeff in enumerate(v):\n",
    "        if coeff != 0:\n",
    "            print(f\"{coeff:+} · [{edge_repr(edges[j])}]\")\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "c65eea9b",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "+1.0 · [(1, 0, 0) ⊂ {x3 = 0}]\n",
      "-1.0 · [(1, 0, 0) ⊂ {x2 = 0}]\n",
      "-1.0 · [(0, 1, 0) ⊂ {x3 = 0}]\n",
      "+1.0 · [(0, 1, 0) ⊂ {x1 = 0}]\n",
      "+1.0 · [(0, 0, 1) ⊂ {x2 = 0}]\n",
      "-1.0 · [(0, 0, 1) ⊂ {x1 = 0}]\n"
     ]
    }
   ],
   "source": [
    "pretty_print_cycle(steinberg_basis[0],edges)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "51d7e616",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "from itertools import permutations\n",
    "\n",
    "def apartment_cycle_from_g(g, edges, edge_index):\n",
    "\n",
    "    basis = [g.matrix().column(i) for i in range(3)]\n",
    "    lines = [V.subspace([v]) for v in basis]\n",
    "\n",
    "    cycle = vector(CDF, len(edges))\n",
    "\n",
    "    for w in permutations([0, 1, 2]):\n",
    "        sign = Permutation([i+1 for i in w]).signature()\n",
    "        p = lines[w[0]]\n",
    "        H = lines[w[0]] + lines[w[1]]\n",
    "        j = edge_index[(p, H)]\n",
    "        cycle[j] += sign\n",
    "\n",
    "    return cycle"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "9cc9bcd6",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "g = G.random_element()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "id": "4d5f4c11",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 1.0, 0.0, -1.0, 0.0, -1.0, 1.0, -1.0, 1.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0)"
      ]
     },
     "execution_count": 20,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "v_A = apartment_cycle_from_g(g, edges, edge_index); v_A"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "id": "85277668",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "True\n"
     ]
    }
   ],
   "source": [
    "#verify this is actually a cycle by multiplying by boundary map\n",
    "print((boundary * v_A).is_zero())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "id": "2bb4a1db",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "+1.0 · [(1, 1, 0) ⊂ {x3 = 0}]\n",
      "-1.0 · [(1, 1, 0) ⊂ {x1 + x2 = 0}]\n",
      "-1.0 · [(1, 1, 1) ⊂ {x1 + x3 = 0}]\n",
      "+1.0 · [(1, 1, 1) ⊂ {x1 + x2 = 0}]\n",
      "-1.0 · [(0, 1, 0) ⊂ {x3 = 0}]\n",
      "+1.0 · [(0, 1, 0) ⊂ {x1 + x3 = 0}]\n"
     ]
    }
   ],
   "source": [
    "pretty_print_cycle(v_A,edges)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 59,
   "id": "91b56087",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "###### DEFINE G-ACTION ON STEINBERG REPRESENTATION #######"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 54,
   "id": "4c24e11f",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "def g_act_on_subspace(g, S):\n",
    "    \"\"\"\n",
    "    Given g in GL_3(F_2) and a subspace S of W,\n",
    "    return g(S) as a subspace of W.\n",
    "    \"\"\"\n",
    "    W = S.ambient_vector_space()\n",
    "    basis = S.basis_matrix().transpose()\n",
    "    transformed_columns = [g * basis.column(i) for i in range(basis.ncols())]\n",
    "    vecs = [W(v) for v in transformed_columns]  # convert each to W-element explicitly\n",
    "    return W.subspace(vecs)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3553a45d",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "def g_action_on_edge_indices(g, edges, edge_index):\n",
    "    \"\"\"\n",
    "    Returns a dictionary mapping edge indices under the action of g.\n",
    "    \"\"\"\n",
    "    new_index_map = {}\n",
    "    for i, (p, H) in enumerate(edges):\n",
    "        gp = g_act_on_subspace(g, p)\n",
    "        gH = g_act_on_subspace(g, H)\n",
    "        j = edge_index[(gp, gH)]\n",
    "        new_index_map[i] = j\n",
    "    return new_index_map"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 56,
   "id": "8697532c",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "def g_action_on_vector(g, v, edges, edge_index):\n",
    "    perm_map = g_action_on_edge_indices(g, edges, edge_index)\n",
    "    # create a zero vector of same dimension\n",
    "    v_new = vector(CDF, len(v))\n",
    "    for i, val in enumerate(v):\n",
    "        if val != 0:\n",
    "            j = perm_map[i]\n",
    "            v_new[j] += val\n",
    "    return v_new"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 58,
   "id": "489417b1",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(1.0, 0.0, -1.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, -1.0, 0.0, 1.0, 0.0, 0.0, 0.0, 1.0, 0.0, -1.0)\n",
      "(0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 1.0, 0.0, -1.0, 0.0, -1.0, 1.0, -1.0, 1.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0)\n"
     ]
    }
   ],
   "source": [
    "v0 = steinberg_basis[0]; print(v0)\n",
    "g_v0 = g_action_on_vector(g, v0, edges, edge_index); print(g_v0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7e229c10",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "# verify the action is in fact a G-action on H(\\Delta; \\C), the Steinberg representation\n",
    "\n",
    "h = G.random_element()\n",
    "lhs = g_action_on_vector(g, g_action_on_vector(h, v0, edges, edge_index), edges, edge_index)\n",
    "rhs = g_action_on_vector(g * h, v0, edges, edge_index)\n",
    "assert lhs == rhs, \"G-action property failed!\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c6557f7d",
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.7",
   "language": "sage",
   "name": "SageMath-10.7"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.13.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
